SCENE character_operations_example
    (GUI textbox changes to "TEXTBOX_NASTYA")
    (GUI namebox changes to "NAMEBOX")
    Nayu: "In this act we're gonna give some examples about the actual functionalities on characters"
    (Nayu appears)
    Nayu: "Let's go!"
    (Nayu disappears)
    (Scene "character_spawning" begins)
CURTAIN

SCENE character_spawning
    Nayu: "First functionality, obviously, regards spawning"
    (Nayu appears)
    Nayu: "This is the most simple example, and it can be achieved by calling appears/disappears in .sabi files"
    Nayu: "Check the docs on syntax file!"
    (Nayu disappears)
    (Nayu fade in)
    Nayu: "Characters can even fade in on the scene and, of course..."
    (Nayu fade out)
    Nayu: "...fade out"
    (Nayu appears right looking left happy)
    Nayu: "This commands can be combined with position declaration, which defines where characters will be spawned"
    Nayu: "Currently supported position: center, left, right, far left, far right, invisible left, invisible right"
    Nayu: "You can even combine it with character direction, which we are gonna see right now"
    (Nayu disappears)
    (Scene "character_direction" begins)
CURTAIN

SCENE character_direction
    Nayu: "Since a character can look left or right, you can easily handle this by calling 'looks'/'looking' commands"
    (Nayu appears)
    Nayu: "We decided to handle character images like they are always provided with a left-to-right direction"
    (Nayu looks left)
    Nayu: "Pay attention to this or your VN could show characters in opposite directions!"
    (Nayu fade out)
    (Scene "character_movement" begins)
CURTAIN

SCENE character_movement
    Nayu: "Why do we need to have among the position something like 'invisible left'?"
    (Nayu appears invisible left)
    (Nayu moves center)
    Nayu: "Because we could let character come into scene from outside"
    Nayu: "Isn't it good?"
    (Nayu moves far left)
    Nayu: "Current position are calculated with percentages over the window size and they are automatically rendered on window resize events"
    Nayu: "Is it good enough? Contribute or open an issue! :)"
    (Nayu looks left)
    (Nayu moves right)
    Nayu: "We're working on this to make it easy to build Visual Novel in Bevy: let us know what you think of it"
    (Nayu fade out)
CURTAIN