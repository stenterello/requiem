WHITESPACE = _{ " " | "\t" | NEWLINE }

// The capsule for the program and the
//  enum for its statements
act = { SOI ~ scene+ ~ EOI }
scene = { "SCENE " ~ scene_name ~ statement* ~ "CURTAIN" }
scene_name = @{ (ASCII_ALPHANUMERIC+ | "_" | "-")+ }

// There are three types of statements
//  1. Code statements for logic, etc
//  2. Stage directions for changing scenes, backgrounds, sounds, etc
//  3. TextItem for characters to say things or for info text
statement = _{
    code |
    stage_command |
    text_item
}

// Stage directions
stage_command = { "(" ~ stage_command_type ~ ")" }
    stage_command_type = _{
        gui_change |
        background_change |
        scene_change |
        act_change |
        character_change |
        animation_change }
    background_change = { background_directive }
    gui_change = { "GUI" ~ gui_element ~ "changes" ~ "to" ~ expr ~ image_mode? }
    scene_change = { "Scene" ~ expr ~ "begins" }
    act_change = { "Act" ~ expr ~ "begins" }
    character_change = { character_name ~ character_action }
    animation_change = { animation_identifier ~ animation_action }

// Code statements
code = { "{" ~ code_statement ~ "}" }
code_statement = _{ log }
// Writes a message to the console
log = { "log " ~ expr+ }

// Text Item
text_item = { dialogue | infotext }

// Info Text
infotext = { narrator ~ ": " ~ expr }

// Makes a character or the MC say something
dialogue = { character_identifier ~ ": " ~ dialogue_emotion_change? ~ expr ~ (expr | stage_command)* }
dialogue_emotion_change = { "(" ~ emotion_name ~ ")" }

// Expressions
expr = { term ~ (infix_op ~ term)* }
term = _{
    string |
    number |
    "(" ~ expr ~ ")"
    }
infix_op = _{ add }
add = { "+" }

// General types
character_identifier = {
    mc_identifier |
    character_name
}
character_name = @{ ASCII_ALPHA_UPPER ~ ASCII_ALPHA_LOWER+ }
emotion_name = @{ ASCII_ALPHA+ }
mc_identifier = { "MC" }
narrator = { "info" }
animation_identifier = { "Animated" ~ expr }
gui_element = { "textbox" | "namebox" }
image_mode = { "sliced" | "auto" }
actor_spawn_directive = {
                            "appears" |
                            "disappears" |
                            "fade in" |
                            "fade out"
                        }
actor_direction_directive = { actor_direction_command ~ actor_direction }
actor_direction_command = { "looking" | "looks" }
actor_direction = { "left" | "right" }
actor_movement_directive = { "moves" }
character_position = { 
                        "center" |
                        "far left" |
                        "far right" |
                        "left" |
                        "right" |
                        "invisible left" |
                        "invisible right"
                     }
animation_position = {
                         "top left" |
                         "top right" |
                         "top" |
                         "left" |
                         "center" |
                         "right" |
                         "bottom left" |
                         "bottom right" |
                         "bottom"
                     }
character_action =   {
                         (actor_spawn_directive ~ character_position? ~ actor_direction_directive? ~ emotion_name?) |
                         (actor_direction_directive) |
                         (actor_movement_directive ~ character_position)
                     }
animation_action =   {
                         (actor_spawn_directive ~ animation_position? ~ actor_direction_directive? ~ animation_scale?) |
                         (actor_movement_directive ~ animation_position)
                     }
animation_scale =    {
                         ("scale" ~ number)
                     }
                   
// Background definitions
background_directive = { "Background" ~ background_action }
background_action = {
                        background_change_def |
                        background_dissolve_def |
                        background_slide_def
                    }
background_change_def   = { ("changes" ~ "to" ~ expr) }
background_dissolve_def = { ("dissolves" ~ ("to" ~ expr)?) }
background_slide_def    = { ("slides" ~ "to" ~ background_direction) }
background_direction    = {
                              "North" |
                              "N" |
                              "South" |
                              "S" |
                              "East" |
                              "E" |
                              "West" |
                              "W"
                          }

// Intrinsic types
number    = @{ "-"? ~ ASCII_DIGIT+ ~ "."* ~ ASCII_DIGIT* }
string    = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
